// Copyright (c) 2013-2014 Turbulenz Limited

#include "particles-common.cgh"

float3 acceleration;
float drag;

sampler2D noiseTexture = sampler_state
{
    MinFilter = LinearMipMapNearest;
    MagFilter = Linear;
    WrapS = Wrap;
    WrapT = Wrap;
};

// strength of randomisation
float3 randomizedAcceleration;

float3 getAccelerationNoise(float2 uv, float t)
{
    // constants are randomised values as are choice of noise channels.
    return (2.0 * tex2D(noiseTexture, uv + float2(0.97303011314943430, 0.19842534395866096) * t
                                         + float2(0.84200346702709790, 0.59638088289648291)).zyx - 1.0
           ) * randomizedAcceleration;
}

// --------------------------------------------------------------

// wrap x to be within -1.0 to 1.0
float wrap(float x)
{
    x = fmod(x + 1.0, 2.0) - 1.0;
    return x + (step(1.0, -x) - step(1.0, x)) * 2.0;
}

float4 fp_update_state_base(bool wrapPositions, float4 wPos)
{
    float2 particle = floor(wPos.xy - regionPos);                  // N^2 [0, regionSize)  (regionSize is 3N)

    // Note: IE11 bug, cannot do * (1.0 / PARTICLE_DIM) as this breaks!
    float2 index = floor(particle / PARTICLE_DIM) * PARTICLE_DIM;  // (3N)^2 [0, regionSize)

    if (createdParticle(index))
    {
        // Newly created particle, no simulation to be performed.
        return tex2D(creationState, (particle + 0.5) * creationScale);
    }

    float2 regionIndex = index + regionPos;
    if (deadParticle(previousState, regionIndex))
    {
        // Dead particle
        return float4(0.0);
    }

    float2 slot = particle - index; // N^2 [0, 3)
    float2 inParticle = wPos.xy * invTextureSize; // (0, 1) uv (1/2) index of fragment in output/previousState.
    if (slot.x == 0.0)
    {
        // position fragment
        float p = decodeSignedFloat(tex2D(previousState, inParticle));
        float v = decodeSignedFloat(tex2D(previousState, inParticle + float2(invTextureSize.x, 0.0)));
        p += (v * timeStep * maxSpeed / halfExtents[slot.y]) + shift[slot.y];
        if (wrapPositions)
        {
            return encodeSignedFloat(wrap(p));
        }
        else
        {
            return encodeSignedFloat(p);
        }
    }
    else if (slot.x == 1.0)
    {
        // velocity fragment
        float v = decodeSignedFloat(tex2D(previousState, inParticle));
        float a = acceleration[slot.y];
        float4 data = getData(previousState, regionIndex);
        if (bitSet(data.w, BIT1))
        {
            float2 noiseUV = decodeByteUnsignedFloat2(data.z);
            float animTime = getAnimationTime(previousState,
                                              regionIndex,
                                              getLifePair(previousState, regionIndex));
            a += getAccelerationNoise(noiseUV, animTime)[slot.y];
        }
        float d = 1.0 - min(1.0, timeStep * drag);
        return encodeSignedFloat(d * (v + a * timeStep / maxSpeed));
    }
    else
    {
        if (slot.y == 0.0)
        {
            // life
            float4 pix = tex2D(previousState, inParticle);
            float life = decodeHalfUnsignedFloat(pix.zw);
            return float4(pix.xy, encodeHalfUnsignedFloat(life - lifeStep));
        }
        else
        {
            // user store.
            return tex2D(previousState, inParticle);
        }
    }
}

float4 fp_update_state(float4 inWPos: WPOS): COLOR
{
    return fp_update_state_base(false, inWPos);
}

float4 fp_update_state_wrap(float4 inWPos: WPOS): COLOR
{
    return fp_update_state_base(true, inWPos);
}

// --------------------------------------------------------------

technique clamped
{
    pass
    {
        DepthTestEnable = false;
        DepthMask       = false;
        CullFaceEnable  = false;
        BlendEnable     = false;
        VertexProgram   = compile latest vp_update();
        FragmentProgram = compile latest fp_update_state();
    }
}

technique wrapped
{
    pass
    {
        DepthTestEnable = false;
        DepthMask       = false;
        CullFaceEnable  = false;
        BlendEnable     = false;
        VertexProgram   = compile latest vp_update();
        FragmentProgram = compile latest fp_update_state_wrap();
    }
}
